
apply plugin: 'com.android.library'
apply from: '../base_module.gradle'
apply plugin: 'kotlin-kapt'

android {

    defaultConfig {
        consumerProguardFiles 'consumer-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }

    }

    buildTypes {
        release {
            buildConfigField 'String', "LAST_FM_KEY", last_fm_key
            buildConfigField 'String', "LAST_FM_SECRET", last_fm_secret
        }
        debug {
            buildConfigField 'String', "LAST_FM_KEY", last_fm_key
            buildConfigField 'String', "LAST_FM_SECRET", last_fm_secret
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

//    kotlinOptions.allWarningsAsErrors = true
}

apply from: '../kapt.gradle'

dependencies {
    implementation project(':core')
    implementation project(':shared')
    implementation project(':prefskeys')

    implementation lib_core.coroutines
    implementation lib_core.coroutines_android

    implementation lib_core.dagger
    kapt lib_core.dagger_processor

    implementation lib_x.core_ktx
    implementation lib_x.preference
    implementation lib_x.lifecycle_extensions

    implementation lib_utils.fuzzy
    implementation lib_network.gson
    implementation lib_network.retrofit
    implementation lib_network.retrofit_gson
    implementation lib_network.ok_http
    implementation lib_network.ok_http_interceptor

    implementation lib_data.sql_content_resolver
    implementation lib_data.room
    implementation lib_data.room_ktx
    kapt lib_data.room_processor

}
